#define TOUCH_MODULES_CST_MUTUAL

// this needs esp32 bsp 2.x (3.x does not compile)

#include "lvgl.h"
#include "misc/lv_color.h"
#include "Arduino_GFX_Library.h"
#include "pin_config.h"
#include "TouchLib.h"
#include "SmartEVSE.h"
#include "UI.h"


unsigned long next_time;

ChargerState charger_state;

#define ADDRESS 1



volatile bool Touch_Int_Flag = false;

TouchLib touch(Wire, TOUCH_SDA, TOUCH_SCL, CST3240_ADDRESS);

Arduino_DataBus *bus = new Arduino_XL9535SWSPI(IIC_SDA /* SDA */, IIC_SCL /* SCL */, -1 /* XL PWD */,
                                               XL95X5_CS /* XL CS */, XL95X5_SCLK /* XL SCK */, XL95X5_MOSI /* XL MOSI */);

Arduino_ESP32RGBPanel *rgbpanel = new Arduino_ESP32RGBPanel(
  -1 /* DE */, LCD_VSYNC /* VSYNC */, LCD_HSYNC /* HSYNC */, LCD_PCLK /* PCLK */,
  LCD_B0 /* B0 */, LCD_B1 /* B1 */, LCD_B2 /* B2 */, LCD_B3 /* B3 */, LCD_B4 /* B4 */,
  LCD_G0 /* G0 */, LCD_G1 /* G1 */, LCD_G2 /* G2 */, LCD_G3 /* G3 */, LCD_G4 /* G4 */, LCD_G5 /* G5 */,
  LCD_R0 /* R0 */, LCD_R1 /* R1 */, LCD_R2 /* R2 */, LCD_R3 /* R3 */, LCD_R4 /* R4 */,
  1 /* hsync_polarity */, 20 /* hsync_front_porch */, 2 /* hsync_pulse_width */, 0 /* hsync_back_porch */,
  1 /* vsync_polarity */, 30 /* vsync_front_porch */, 8 /* vsync_pulse_width */, 1 /* vsync_back_porch */,
  10 /* pclk_active_neg */, 6000000L /* prefer_speed */, false /* useBigEndian */,
  0 /* de_idle_high*/, 0 /* pclk_idle_high */);

Arduino_RGB_Display *gfx = new Arduino_RGB_Display(
  LCD_WIDTH /* width */, LCD_HEIGHT /* height */, rgbpanel, 0 /* rotation */, true /* auto_flush */,
  bus, -1 /* RST */, st7701_type9_init_operations, sizeof(st7701_type9_init_operations));

static lv_disp_draw_buf_t draw_buf;
static lv_disp_drv_t disp_drv;

/* Display flushing */
void my_disp_flush(lv_disp_drv_t *disp, const lv_area_t *area, lv_color_t *color_p) {

  uint32_t w = (area->x2 - area->x1 + 1);
  uint32_t h = (area->y2 - area->y1 + 1);

  gfx->draw16bitRGBBitmap(area->x1, area->y1, (uint16_t *)&color_p->full, w, h);

  lv_disp_flush_ready(disp);
}

/*Read the touchpad*/
void my_touchpad_read(lv_indev_drv_t *indev_drv, lv_indev_data_t *data) {

  if (Touch_Int_Flag == true) {

    touch.read();
    TP_Point t = touch.getPoint(0);

    if ((touch.getPointNum() == 1) && (t.pressure > 0) && (t.state != 0)) {

      data->state = LV_INDEV_STATE_PR;

      /*Set the coordinates*/
      data->point.x = t.x;
      data->point.y = t.y;

      // Serial.printf("Touch X: %d Y: %d", t.x, t.y);
      // Serial.printf("Static: %d", t.state);
      // Serial.printf("Pressure: %d", t.pressure);
    }

    Touch_Int_Flag = false;

  } else {

    data->state = LV_INDEV_STATE_REL;
  }
}

void lvgl_initialization(void) {

  lv_init();

  uint32_t screen_width = gfx->width();
  uint32_t screen_height = gfx->height();

  lv_color_t *buf_1 = (lv_color_t *)heap_caps_malloc(sizeof(lv_color_t) * LCD_WIDTH * 40, MALLOC_CAP_INTERNAL | MALLOC_CAP_8BIT);
  lv_color_t *buf_2 = (lv_color_t *)heap_caps_malloc(sizeof(lv_color_t) * LCD_WIDTH * 40, MALLOC_CAP_INTERNAL | MALLOC_CAP_8BIT);

  while ((!buf_1) || (!buf_2)) {

    Serial.println("LVGL disp_draw_buf allocate failed!");
    delay(1000);
  }

  lv_disp_draw_buf_init(&draw_buf, buf_1, buf_2, LCD_WIDTH * 40);

  /* Initialize the display */
  lv_disp_drv_init(&disp_drv);
  /* Change the following line to your display resolution */
  disp_drv.hor_res = screen_width;
  disp_drv.ver_res = screen_height;
  disp_drv.flush_cb = my_disp_flush;
  disp_drv.draw_buf = &draw_buf;
  disp_drv.full_refresh = 1;
  lv_disp_drv_register(&disp_drv);

  /*Initialize the (dummy) input device driver*/
  static lv_indev_drv_t indev_drv;
  lv_indev_drv_init(&indev_drv);
  indev_drv.type = LV_INDEV_TYPE_POINTER;
  indev_drv.read_cb = my_touchpad_read;
  lv_indev_drv_register(&indev_drv);
}

void ui_set_advertizing_current_callback(int advertizing_current) {

    Serial.printf("advertizing %dA\r\n", advertizing_current);
}

void ui_set_phases_callback(int phases) {

    Serial.printf("set phases to %d\r\n", phases);
}

void setup() {

  Serial.begin(115200);

  Serial.printf("\r\nLiliGo  T-Panel LVGL\r\n\r\n");

  pinMode(LCD_BL, OUTPUT);
  digitalWrite(LCD_BL, HIGH);

  attachInterrupt(
    TOUCH_INT,
    [] {
      Touch_Int_Flag = true;
    },
    FALLING);

  Wire.begin(IIC_SDA, IIC_SCL);

  gfx->begin();
  gfx->fillScreen(BLACK);

  gfx->XL_digitalWrite(XL95X5_TOUCH_RST, LOW);
  delay(200);
  gfx->XL_digitalWrite(XL95X5_TOUCH_RST, HIGH);
  delay(200);

  touch.init();

  lvgl_initialization();

  smart_evse_init();
  charger_state.count = 0;
  charger_state.error_count = 0;


  int board_max_current = 16;

  if (smart_evse_is_32_amp(ADDRESS)) {
    board_max_current = 32;
  }

  ui_init(board_max_current);

  Serial.printf("init done\r\n");

  Serial.printf("Request serial and fw version:\r\n");



  smart_evse_get_serial(ADDRESS);

  smart_evse_get_fw_version(ADDRESS);

  smart_evse_get_max_currents(ADDRESS);

  next_time = millis() + 1000;
}



void loop() {

  unsigned long now = millis();

  lv_timer_handler();
  delay(5);

  if (now > next_time) {

    next_time = now + 1000;

    smart_evse_get_charger_state(ADDRESS, &charger_state);

    if (charger_state.isNew) {

      charger_state.isNew = false;

      Serial.printf("state: %d, error: %d, temperature: %d, cable: %d a\r\n",
                    charger_state.state, charger_state.error, charger_state.temperature, charger_state.cable_current);
      Serial.printf("current: %f, %f, %f a\r\n", charger_state.c[0], charger_state.c[1], charger_state.c[2]);
      Serial.printf("voltage: %f, %f, %f v\r\n", charger_state.v[0], charger_state.v[1], charger_state.v[2]);
      Serial.printf("session: %f kwh, total: %f kwh\r\n\r\n",charger_state.kwh_session, charger_state.kwh_total);

      ui_set_ev_values(charger_state);
    }
  }
}